grammar calc;

@def { double val = 0.0; }
@start expr

expr -> expr '+' term;
@attr { @it.val = @0.val + @2.val; }
expr -> expr '-' term;
@attr { @it.val = @0.val - @2.val; }
expr -> term;
@attr { @it.val = @0.val; }

term -> term '*' factor;
@attr { @it.val = @0.val * @2.val; }
term -> term '/' factor;
@attr { @it.val = @0.val / @2.val; }
term -> factor;
@attr { @it.val = @0.val; }

factor -> \d+ ('.' \d+)?;
@attr { @it.val = Double.parseDouble(@0.string); }
factor -> 'pi';
@attr { @it.val = Math.PI; }
factor -> '(' expr ')';
@attr { @it.val = @1.val; }

factor -> 'sin(' expr ')';
@attr { @it.val = Math.sin(@1.val); }

factor -> 'cos(' expr ')';
@attr { @it.val = Math.cos(@1.val); }

factor -> '|' expr '|';
@attr { @it.val = Math.abs(@1.val); }

factor -> '[' expr ']';
@attr { @it.val = Math.ceil(@1.val); }

skip -> ' ';